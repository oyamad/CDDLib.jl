_getrepfor(p::Polyhedron{T}, ::Polyhedra.Incident{T, <:HRepElement{T}}) where {T} = getine(p)
_getrepfor(p::Polyhedron{T}, ::Polyhedra.Incident{T, <:VRepElement{T}}) where {T} = getext(p)
_getincidence(p::Polyhedron{T}, ::Polyhedra.Incident{T, <:HRepElement{T}}) where {T} = gethincidence(p)
_getincidence(p::Polyhedron{T}, ::Polyhedra.Incident{T, <:VRepElement{T}}) where {T} = getvincidence(p)
_incel(p::Polyhedron{T}, inc::Polyhedra.IncidentElements{T}, idx) where {T} = get(p, idx)
_incel(p::Polyhedron{T}, inc::Polyhedra.IncidentIndices{T}, idx) where {T} = idx

function Base.get(p::CDDLib.Polyhedron{T}, inc::Polyhedra.Incident{T, ElemT}; tol) where {T, ElemT}
    rep = _getrepfor(p, inc)
    incidence = _getincidence(p, inc)
    incT = Polyhedra._inctype(inc)
    incs = incT[]
    for i in incidence[inc.idx.value]
        idx = Polyhedra.Index{T, ElemT}(i)
        isvalid(rep, idx) && push!(incs, _incel(p, inc, idx))
    end
    return incs
end
